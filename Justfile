set quiet := true
set dotenv-load := true

# Dev commands
##############

# Print available commands
help:
    just _help

# Enter development
dev: mcp-run
    cursor .

# Run tests
[no-quiet]
test:
    cd packages/compiler && just test
    cd packages/ide-extension && just test
    cd packages/tao-cli && just test
    cd packages/shared-tools && bun test
    cd packages/expo-runtime && just test

# Check that builds work
check-builds:
    cd packages/tao-cli && just build && ./.builds/tao help

# Run and Build commands
########################

# Start tao expo runtime
start-runtime:
    cd packages/expo-runtime && just run

# Run Tao Studio app
run-studio:
    cd packages/tao-cli && bun run tao-cli.ts run-app "Apps/Tao Studio/Tao Studio.tao"

# Build and run Tao CLI with given arguments
tao args:
    just build
    echo "\n> Run: ./.builds/tao-cli {{ args }}\n"
    ./.builds/tao-cli {{ args }}
    echo

# Build everything
[no-quiet]
build:
    cd packages/compiler && just build
    cd packages/ide-extension && just build
    cd packages/tao-cli && just build

# Format and check files
fmt:
    just _fmt

# Check all code: lint, typecheck, etc.
check:
    just _check

# Setup development environment
setup:
    echo "\tSetup Tao Lang dev environment:"
    just _install_mise
    just _do_setup

reload-extension:
    cd packages/ide-extension && just run

# LLM & Agent Setups
####################

# Run mise MCP server
mcp-run:
    screen -dmS mise-mcp mise mcp

# Halt mise MCP server
mcp-halt:
    screen -X -S mise-mcp quit

# Generate mise-gen-just-commands.toml from this file
gen-mise-tasks:
    cd packages/shared-tools && bun tools-src/gen-mise-tasks.ts

############
# Internal #
############

[private]
_MISE_CMD := "~/.local/bin/mise"

# Formatting and linting
########################

_fmt:
    just --fmt --unstable 1> /dev/null
    bunx sort-package-json packages/*/package.json 1> /dev/null
    dprint fmt 1> /dev/null
    {{ _MISE_CMD }} fmt 1> /dev/null
    just check
    oxlint --fix 1> /dev/null

[no-quiet]
_check:
    oxlint
    cd packages/compiler && tsc --noEmit
    cd packages/tao-cli && tsc --noEmit
    cd packages/shared-tools && tsc --noEmit
    cd packages/expo-runtime && tsc --noEmit
    cd packages/expo-runtime && bunx expo lint

# Help & Setup
##############

_help:
    echo "\nTo ended Tao Dev Env, run either of:\n"
    echo "    enter-tao"
    echo "    et"
    echo "\n For more commands, run:\n"
    echo "    just <recipe>"
    echo
    just --list --unsorted
    echo

_do_setup:
    #!/bin/zsh
    set -e
    eval "$({{ _MISE_CMD }} activate zsh)"
    just _configure_mise
    just _configure_zsh
    just _install_deps
    just _install_ide_extensions
    just _print_setup_done

_install_mise:
    @ echo "\tInstall mise..."
    @ curl -sS https://mise.run | MISE_VERSION=v2025.12.1 sh 2> /dev/null
    @ {{ _MISE_CMD }} trust --quiet

_configure_mise:
    @ echo "\tConfigure mise..."
    @ mise trust --quiet
    @ mise install 2> /dev/null
    @ mise doctor 1> /dev/null

_configure_zsh:
    #!/bin/zsh
    set -e
    echo "\tConfigure zsh..."
    add_if_missing() { local line="$1" file="$2"; grep -qxF "$line" "$file" || echo "\n# Added by tao-lang dev env '_configure_zsh' justfile rule\n$line" >> "$file"; }
    add_if_missing "alias et=enter-tao && alias enter-tao='echo \" * Enter Tao Lang dev env * \" && cd $PWD && source .config/enter-tao-dev-env-source-configure.zsh'" ~/.zshrc

_install_deps:
    @ echo "\tInstall dependencies..."
    @ curl https://cursor.com/install -fsS | bash > /dev/null 2>&1
    @ mise install 2> /dev/null

_install_ide_extensions:
    @ echo "\tInstall IDE extensions..."
    @ jq -r '.recommendations[]' .vscode/extensions.json | xargs -L 1 cursor --force --install-extension 1> /dev/null

_print_setup_done:
    @ echo "\n\t*** Done ***"
    @ echo "\trestart your terminal and run:\n"
    @ echo "\t\tenter-tao && just help"
    @ echo

# Agent Generated Commands
##########################
# These are commands generated by agents, to be used by the agents themselves.
# If a command is needed that doesn't exist, they need to ask for permission before adding it.

# Run tests
_agent-test: test

# Format and lint code
_agent-fmt: fmt

# Lint code
_agent-lint: check

# Generate mise-gen-just-commands.toml from this file
_agent-gen-mise-tasks: gen-mise-tasks

# Execute git commands:
_agent-git args:
    git {{ args }}

# Execute mise commands:
_agent-mise args:
    mise {{ args }}
