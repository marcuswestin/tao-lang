set fallback
set dotenv-load
set quiet

test:
    # No tests currently

dev:
    cursor --extensionDevelopmentPath .

run:
    echo
    echo "\tBuild and package extension..."
    bunx @vscode/vsce package --allow-missing-repository --out ../../.builds 2>&1 > /dev/null
    echo "\tInstall extension..."
    just uninstall-extension > /dev/null 2>&1 || true
    just install-extension > /dev/null 2>&1
    echo "\tCheck extension installation..."
    set -e && cursor --list-extensions | grep "tao.tao-lang-vscode-extension" || { echo "Installation failed -- extension not found" >&2; exit 1; }
    echo "\n\t* Extension installed - restart IDE *\n"
    just restart-cursor

uninstall-extension:
    cursor --uninstall-extension tao.tao-lang-vscode-extension

install-extension:
    cursor --install-extension ../../.builds/tao-lang-vscode-extension-0.0.1.vsix --verbose --force

restart-cursor:
    osascript -e 'quit app "Cursor"'
    sleep 0.25 && open -a Cursor && sleep 0.5 && open -a Cursor && sleep 0.5 && open -a Cursor

[no-quiet]
build: _install_deps
    cd ../compiler && just gen
    tsc -b tsconfig.json
    node esbuild.mjs

watch:
    bunx concurrently -n tsc,esbuild -c blue,yellow \
        "tsc -b tsconfig.json --watch" \
        "node esbuild.mjs --watch"

_install_deps:
    bun install
